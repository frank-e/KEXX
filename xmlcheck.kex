'set novalue on'  /* force KEXX and its way of SIGNAL ON NOVALUE  */

/* Usage:         [MACRO] XMLCHECK                                */
/* Purpose:       Check that the edited file is well formed XML.  */
/*                The file is not modified.  XMLCHECK finds tag   */
/*                nesting errors, syntactically invalid NMTOKENs, */
/*                syntactically invalid attributes (assuming type */
/*                CDATA for attribute values), erroneous numeric  */
/*                character references (NCRs), any "&" outside of */
/*                entities, and similar issues.                   */
/* Caveats:       HTML is not XML, do not use XMLCHECK for HTML.  */
/*                The handling of "<!DOCTYPE ... >" even without  */
/*                DTD subset is incomplete.  "<![CDATA[ ... ]]>"  */
/*                and "<!-- ... -->" comments work as expected.   */
/*                For end tags "</xyz>" white space after "</" is */
/*                not yet supported and could be in fact invalid. */
/*                White space including line breaks after "<" or  */
/*                before ">" is ignored.  This might be invalid   */
/*                within "<!", "<?", or "?>".                     */
/* Unsupported:   "<![IGNORE[ ... ]]>" and "<![INCLUDE ... ]]>"   */
/*                are only used in DTDs or DTD subsets.  XMLCHECK */
/*                does not check the syntax of "<!ATTLIST ... >", */
/*                "<!ELEMENT ... >", or "<!ENTITY ... >" in DTDs. */
/* KEDIT limit:   DTD subsets and tags longer than the line WIDTH */
/*                setting will fail; see KHELP WIDTH for details. */
/* Procedures:       BOMB     accepts u+FEFF at begin of 1st line */
/*                   CDATA    checks all NCRs, '&', and '<'       */
/*                   CONTROL  accepts '09'x, '0A'x, and '0D'x     */
/*                   NMTOKEN  accepts letters, ':_', digits, '-.' */
/*                   PARAM    checks DTD parameter entity names   */
/*                   SUBSET   checks "<! ... >" in DTD subsets    */
/*                   UNWELL   reports missing closing tag source  */
/*                   GARBAGE  reports unexpected input data       */
/*                   FINDME   matches wanted string (or next '>') */
/* See also:      <URL:http://purl.net/xyzzy/kex/xmlcheck.kex>    */
/*                <URL:http://purl.net/xyzzy/src/xmlcheck.rex>    */
/* Requires:      Kedit 5.0 or KeditW 1.0 (Frank Ellermann, 2008) */

   XCTL = xrange( x2c( 0E ), x2c( 1F )) || x2c( 7F )
   XCTL = xrange( x2c( 0B ), x2c( 0C )) || XCTL
   XCTL = xrange( x2c( 00 ), x2c( 08 )) || XCTL
   D.0 = 0                             /* number of open D.N tags */
   L.0 = 0                             /* max. tag nesting level  */
   ETAG = 0                         ;  ROOT = ''
   WANT = '<'                       ;  NEXT = ''

   /* KEDIT magic ----------------------------------------------- */
   KROW = line.1()                  ;  KCOL = column.1()
   do LINE = 1
      'nomsg locate :' || LINE      ;  if focuseof()  then  leave
      DATA = curline.3()            ;  'refresh'
   /* ----------------------------------------------- KEDIT magic */

      N = words( NEXT )             ;  MORE = ''
      if N > 0 then  do
         N = wordindex( NEXT, N )   ;  MORE = substr( NEXT, N )
         NEXT = left( NEXT, N - 1 )
      end
      do until DATA = ''               /* remove spaces after '<' */
         parse var DATA WORD DATA      /* '[', or before '>', ']' */
         X = pos( right( MORE, 1 ), '<[' )
         N = pos(  left( WORD, 1 ), '>]' )
         if sign( X + N )  then  MORE = MORE || WORD
                           else  MORE = strip( MORE WORD )
      end

      if NEXT <> ''  then  do          /* get rid of old NEXT for */
         if WANT = '<'     then  do    /* comments or text nodes  */
            if       D.0 > 0        then  call CDATA NEXT, XCTL
            else  if ROOT = '.DTD'  then  call PARAM NEXT
            else  if BOMB( NEXT )   then  nop
            else  exit GARBAGE( NEXT 'garbage outside of element' )
            NEXT = ''                  /* text node must be CDATA */
         end
         else  if WANT = '-->'               then  do
            if pos( '--', NEXT ) > 0   then  exit GARBAGE( '--' )
            NEXT = ''                  /* no '--' in XML comments */
         end
         else  if WANT = ']]>' | WANT = '?>' then  do
            call CONTROL NEXT, XCTL ;  NEXT = ''
         end                           /*  preserve anything else */
      end                              /* until WANT string found */

      NEXT = NEXT || MORE
      parse value FINDME( WANT, NEXT ) with STOP DOCT ',' WANT

      do while sign( STOP )            /* found next WANT string: */
         parse var NEXT DATA (WANT) NEXT
         DATA = strip( DATA )       ;  NEXT = strip( NEXT )

         if DATA <> '' & WANT = '<' then  do
            if       D.0 > 0        then  call CDATA DATA, XCTL
            else  if ROOT = '.DTD'  then  call PARAM DATA
            else  if BOMB( DATA )   then  nop
            else  exit GARBAGE( DATA 'garbage outside of element' )
         end
         if       WANT = '<'  then  do
            if       NEXT = '!' | NEXT = '!['   then  do
               NEXT = '<' || NEXT   ;  leave
            end                        /* very dubious line break */
            else  if abbrev( NEXT, '![CDATA[' ) then  WANT = ']]>'
            else  if abbrev( NEXT, '!--' )      then  do
               NEXT = substr( NEXT, 4 )         ;     WANT = '-->'
            end
            else  if abbrev( NEXT, '!DOCTYPE' ) then  do
               if substr( NEXT, 9, 1 ) <> ' '
                  then  exit GARBAGE( '<' || NEXT )
               if L.0 = 0 & ROOT <> '.DTD'
                  then  L.0 = -1       /* if unexpected <!DOCTYPE */
                  else  exit GARBAGE( '<' || NEXT '- dupe' )
               NEXT = substr( NEXT, 9 )         ;     WANT = ' ['
            end
            else  if abbrev( NEXT, '!' )        then  do
               if L.0 = 0  then  ROOT = '.DTD'  ;     WANT = '>'
               if L.0 > 0  then  exit GARBAGE( '<' || NEXT )
               N = word( NEXT, 1 )  ;  D.1 = '<' || N
               N = wordpos( N, '!ATTLIST !ELEMENT !ENTITY' )
               if N = 0    then  exit GARBAGE( '<' || NEXT )
            end
            else  if abbrev( NEXT, '?' )        then  WANT = '?>'
            else  if NEXT = ''                  then  do
               NEXT = '<' || NEXT   ;  leave
            end                        /* fetches the missing tag */
            else  if ROOT = '.DTD'     /* cannot mix .DTD and XML */
               then  exit GARBAGE( '<' || NEXT 'after' D.1 )
            /* else expecting ordinary XML <tag>, <tag />, </tag> */
            else  if D.0 = 0 & L.0 > 0 /* too many root elements: */
               then  exit GARBAGE( '<' || NEXT '- got already' D.1 )
            else  if abbrev( NEXT, '/' ) = 0    then  do
               N = D.0 + 1          ;  D.N = NMTOKEN( NEXT )
               D.0 = N              ;  L.0 = max( D.0, L.0 )
               L.N = LINE           ;                 WANT = '='
               NEXT = substr( NEXT, 1 + length( D.N ))
            end                        /* got NMTOKEN of open tag */
            else  if D.0 = 0           /* missing a root element: */
               then  exit GARBAGE( '<' || NEXT '- missing root' )
            else  do                   /* match the last open tag */
               N = D.0              ;  D.0 = N - 1
               ETAG = 1             ;                 WANT = '>'
               if abbrev( NEXT, '/' || D.N ) = 0
                  then  exit UNWELL( D.N, L.N )
               NEXT = substr( NEXT, 2 + length( D.N ))
            end
         end
         else  if WANT <> '>' then  do
            if       WANT = '"' | WANT = "'"    then  do
               call CDATA DATA, XCTL            ;     WANT = '='
            end
            else  if WANT = '='                 then  do
               if NMTOKEN( DATA ) <> DATA
                  then  exit GARBAGE( DATA || '=' || NEXT )
               if NEXT = ''   then  do
                  NEXT = DATA '='   ;  leave
               end                     /* fetches attribute value */
               WANT = left( NEXT, 1 )
               if WANT = '"' | WANT = "'"
                  then  NEXT = substr( NEXT, 2 )
                  else  exit GARBAGE( DATA || '=' || NEXT )
            end
            else  if WANT = ']>'                then  do
               call SUBSET DATA, XCTL        ;     WANT = '<'
            end
            else  if CONTROL( DATA, XCTL )      then  OOPS = 0 / 0
            else  if WANT = ' ['                then  do
               ROOT = word( DATA, 1 )           ;     WANT = ']>'
            end
            else  if WANT = '-->'               then  do
               if pos( '--', DATA ) = 0         then  WANT = '<'
                  else  exit GARBAGE( '--' )
            end
            else  if WANT = ']]>'               then  WANT = '<'
            else  if WANT = '?>'                then  WANT = '<'
         end
         else  do                   ;                 WANT = '<'
            if       ETAG        then  do
                  if DATA <> ''  then  exit GARBAGE( DATA || '>' )
                  ETAG = 0             /* end tag has to be empty */
            end
            else  if DOCT | ROOT = '.DTD' then  do
                  if DOCT  then  ROOT = word( DATA, 1 )
                  call CDATA DATA, XCTL
            end
            else  if DATA = '/'  then  D.0 = D.0 - 1
            else  if DATA <> ''  then  exit GARBAGE( DATA || '>' )
         end
         parse value FINDME( WANT, NEXT ) with STOP DOCT ',' WANT
      end
   end

   N = D.0
   if       N > 0       then  exit UNWELL( D.N. L.N )
   else  if WANT <> '<' then  exit GARBAGE( ': missing' WANT )
   else  if L.0 = 0     then  if ROOT = '.DTD'  then  nop
                        else  exit GARBAGE( ': no XML elements' )
   else  if ROOT = ''   then  ROOT = 'XML'
   else  if L.0 < 0     then  exit GARBAGE( ': found no' ROOT )
   else  if ROOT <> D.1 then  exit GARBAGE( D.1 '- expected' ROOT )

   if ROOT <> '.DTD'                   /* intentional dot in .DTD */
      then  N = 'max.' || right( L.0, 3 ) 'nested tags in'
      else  N = 'this is'
   say N 'an apparently well-formed' ROOT 'file'

   /* KEDIT magic ----------------------------------------------- */
   'locate :' KROW 'cl :' KCOL      ;  'cursor column'
   /* ----------------------------------------------- KEDIT magic */
   exit 0

BOMB:    procedure                     /* accept BOM u+FEFF if in */
   if line.1() > 1                     then  return 0 /* 1st line */
   if arg( 1 ) = x2c( 'EFBBBF' )       then  return 1 /* if UTF-8 */
   if arg( 1 ) = x2c( '849F9E9F9F' )   then  return 1 /* if UTF-4 */
   return 0                            /* other UTFs fail anyway  */

CDATA:   procedure                     /* check entities and '<': */
   parse arg DATA, XCTL             ;  POS = pos( '&', DATA ) + 1
   do while POS > 1
      DATA = substr( DATA, POS )    ;  POS = pos( ';', DATA ) + 1
      if POS > 1  then  ENT = left( DATA, POS - 2 )
                  else  ENT = ''       /* missing ';' fails below */
      DATA = substr( DATA, POS )    ;  POS = pos( '&', DATA ) + 1
                                       /* get number of hex. NCR: */
      if       abbrev( ENT, '#x' )  then  do
            T = translate( substr( ENT, 3 ), '.', ' ' )
            if datatype( T, 'x' )   then  T = x2d( T )
                                    else  T = 0
      end                              /* get number of dec. NCR: */
      else  if abbrev( ENT, '#'  )  then  do
            T = translate( substr( ENT, 2 ), '..', '+-' )
            if datatype( T, 'w' )   then  T = T + 0
                                    else  T = 0
      end                              /* otherwise test NMTOKEN: */
      else  T = 10 * ( ENT = NMTOKEN( ENT ))
                                       /* 0: bad token, 10: valid */
      if wordpos( T, '0 9 10 13 133' ) = 0   then  do
        if       T < 32             then  T = 0    /* 0000...001F */
        else  if T < 127            then  nop
        else  if T < 160            then  T = 0    /* 007F...009F */
        else  if T < 55296          then  nop
        else  if T < 57344          then  T = 0    /* D800...DFFF */
        else  if T < 64976          then  nop
        else  if T < 65008          then  T = 0    /* FDD0...FDFF */
        else  if T // 65536 > 65533 then  T = 0    /* FFFE...FFFF */
        else  if T <= 1114111       then  nop
        else                              T = 0    /* if > 10FFFF */
      end
      if T = 0 then  exit GARBAGE( '&' || ENT )
   end

   DATA = arg( 1 )                  ;  POS = pos( '<', DATA )
   if POS = 0  then  return CONTROL( DATA, XCTL )
               else  exit GARBAGE( DATA )

CONTROL: procedure                     /* reject US-ASCII control */
   parse arg DATA, XCTL
   N = verify( DATA, XCTL, 'M' )    ;  if N = 0 then  return 0
   N = c2x( substr( DATA, N, 1 ))   ;  exit GARBAGE( '0x' || N )

GARBAGE: procedure                     /* report any other error: */
   'emsg unexpected' arg( 1 )       ;  return 1

UNWELL:  procedure                     /* report invalid nesting: */
   'emsg unnmatched <' || arg( 1 ) || '> from line' arg( 2 )
   return 1

SUBSET:  procedure                     /* check given DTD subset: */
   parse arg SRC, XCTL              ;  POS = pos( '<!', SRC )
   do while POS > 0
      TOP = left( SRC, POS - 1 )    ;  SRC = substr( SRC, POS + 2 )
      if TOP <> ''   then  call PARAM TOP

      if abbrev( SRC, '--' )  then  do
         parse var SRC '--' TOP '--' SRC
         POS = pos( '>', SRC )
         if POS = 0  then  exit GARBAGE( '<!--' TOP '--' SRC )
         call CONTROL TOP, XCTL     ;  TOP = left( SRC, POS - 1 )
         if TOP <> ''   then  exit GARBAGE( '--' TOP '>' )
         SRC = substr( SRC, POS + 1 )
         POS = pos( '<!', SRC )     ;  iterate
      end

      parse var SRC TOP ' ' SRC
      if wordpos( TOP, 'ATTLIST ELEMENT ENTITY' ) > 0 then  do
         POS = pos( '>', SRC )
         if POS = 0  then  exit GARBAGE( '<!' || TOP SRC )
         TOP = left( SRC, POS - 1 ) ;  SRC = substr( SRC, POS + 1 )
         call CDATA TOP, XCTL       ;  POS = pos( '<!', SRC )
      end
      else  exit GARBAGE( '<!' || TOP '(not implemented)' )
   end
   if SRC = '' then  return         ;  else  return PARAM SRC

PARAM:   procedure                     /* accept parameter entity */
   parse arg DATA                   ;  DATA = strip( DATA )
   do forever
      parse var DATA X 2 P DATA     ;  N = length( P )
      if X <> '%' | N < 2                 then  leave
      X = right( P, 1 )             ;  P = left( P, N - 1 )
      if X <> ';' | ( P <> NMTOKEN( P ))  then  leave
      if DATA = ''                        then  return
   end
   exit GARBAGE( arg( 1 ) 'is no parameter entity' )

NMTOKEN: procedure                     /* assume tags are tokens: */
   WORD = translate( arg( 1 ), 'XX99', ':_-.' )
   if datatype( left( WORD, 1 ), 'M' ) = 0
      then  exit GARBAGE( arg( 1 ) '- expected NMTOKEN' )

   do N = 2 to length( WORD )          /* letters, digits, ':_-.' */
      if datatype( substr( WORD, N, 1 ), 'A' ) = 0 then  leave
   end
   return left( arg( 1 ), N - 1 )

FINDME:  procedure                     /* find next wanted string */
   parse arg WANT, TEXT             ;  WPOS = pos( WANT, TEXT )
   DOCT = ( WANT = ' [' )           ;  DPOS = 0
   if WANT = '=' | DOCT then  DPOS = pos( '>' , TEXT )

   if       DPOS = 0    then  return WPOS DOCT || ',' || WANT
   else  if WPOS = 0    then  return DPOS DOCT || ',' || '>'
   else  if WPOS < DPOS then  return WPOS DOCT || ',' || WANT
   else                       return DPOS DOCT || ',' || '>'
